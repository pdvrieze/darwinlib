pluginManagement {
    repositories {
        jcenter()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case "com.android.library":
                case "com.android.application":
                    def ver = requested.version != null ? requested.version : androidBuildToolsVersion
                    useModule("com.android.tools.build:gradle:${ver}");
                    break
                case "kotlin-android":
                case "org.jetbrains.kotlin.android":
                case "kotlin-android-extensions":
                    def ver = requested.version != null ? requested.version : kotlin_version
                    useModule("org.jetbrains.kotlin:kotlin-gradle-plugin:${ver}");
                    break
                case "org.jetbrains.kotlin.multiplatform":
                    def ver = requested.version != null ? requested.version : kotlin_version
                    useModule("org.jetbrains.kotlin:org.jetbrains.kotlin.multiplatform:${ver}");
                    break
                case "org.jetbrains.dokka-android":
                    def ver = requested.version != null ? requested.version : dokkaVersion
                    useModule("org.jetbrains.dokka:dokka-android-gradle-plugin:${ver}")
                    break
                case "kotlinx-serialization":
                    def version = (requested.version == null || requested.version.length() == 0) ? kotlin_version : requested.version
                    useModule("org.jetbrains.kotlin:kotlin-serialization:$version")
            }
        }
    }
}


rootProject.name="darwinlib"
